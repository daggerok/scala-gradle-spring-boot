/*
    gradle clean build bootRun
    gradle clean app
*/

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVer",
                  "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsPluginVer"
    }
}

plugins {
    id "io.spring.dependency-management" version '0.5.6.RELEASE'
    id 'net.saliman.properties' version '1.4.5'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'com.github.ben-manes.versions'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = sourceCompatibility

group = group

jar {
    version = version
    baseName = baseName
}

repositories { jcenter() }

// gradle bootRun -Dserver.context-path=/app
bootRun { systemProperties = System.properties }

dependencies {
    compile "org.scala-lang:scala-library:$scalaVer",
            "org.slf4j:slf4j-api:$slf4jVer",
            'org.springframework.boot:spring-boot-starter-mustache',
            'org.springframework.boot:spring-boot-devtools',
            "org.webjars:bootstrap:$bootstrapVer"
    
    testCompile "junit:junit:$jUnitVer",
                "org.scalactic:scalactic_$scalatestVer",
                "org.scalatest:scalatest_$scalatestVer",
                "org.specs2:specs2_$specs2Ver",
                'org.springframework.boot:spring-boot-starter-test'
}

task app(type: JavaExec, dependsOn: classes) {
    main = 'com.daggerok.scala.spring.Application'
    classpath sourceSets.main.runtimeClasspath,
              configurations.runtime
}

tasks.withType(ScalaCompile) {
    configure(scalaCompileOptions.forkOptions) {
        memoryMaximumSize = '1g'
        jvmArgs = ['-XX:MaxPermSize=512m']
    }
}

task wrapper(type: Wrapper) { gradleVersion = gradleVersion }

//apply plugin: 'application'
//mainClassName = 'com.daggerok.scala.spring.Application'
//run { systemProperties = System.properties }
